# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/dev

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -e ~/.localrc ]]
then
  source ~/.localrc
fi

# Load antigen if present
if [[ -e $ZSH/antigen/antigen ]]
then
  source $ZSH/antigen/antigen
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load nodenv/pyenv/rbenv/jenv/etc first
source $ZSH/node/nodenv.zsh
source $ZSH/python/pyenv.zsh
source $ZSH/ruby/rbenv.zsh
source $ZSH/java/jenv.zsh

# load everything but the path, completion, install and *env manager files
for file in ${${${${${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/install.zsh}:#*/nodenv.zsh}:#*/pyenv.zsh}:#*/rbenv.zsh}:#*/jenv.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
echo Loading custom completions scripts..
for file in ${(M)config_files:#*/completion.zsh}
do
  echo "  $file"
  source $file
done

unset config_files
